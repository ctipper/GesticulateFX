; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; 
; Copyright (c) 2026 Christopher Tipper
; 
; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
; 
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
; 
; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

#define AppName "GesticulateFX"
#define AppPublisher "e-conomist"
#define AppExeName "GesticulateFX.exe"
#define AppURL "https://www.e-conomist.me.uk/"

#ifndef MyAppVersion
  #define FileVerStr GetFileVersion(AppExeName)
  #define AppVerStr FileVerStr
  #define VersionSuffix ""
#else
  #define AppVerStr MyAppVersion
  #define FileVerStr StringChange(MyAppVersion, "-SNAPSHOT", "")
  // Extract the suffix (everything after the hyphen)
  #if Pos("-", MyAppVersion) > 0
    #define VersionSuffix Copy(MyAppVersion, Pos("-", MyAppVersion), 99)
  #else
    #define VersionSuffix ""
  #endif
#endif

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
AppId={{99F4279F-E0A4-4D5F-BDE4-668FBEDFE9ED}
AppName={#AppName}
AppVersion={#AppVerStr}
AppVerName={#AppName} {#AppVerStr}
UninstallDisplayName={#AppName} {#AppVerStr}
VersionInfoVersion={#FileVerStr}
VersionInfoTextVersion={#AppVerStr}
OutputBaseFilename={#AppName}_Setup_{#FileVerStr}{#VersionSuffix}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppURL}
AppSupportURL={#AppURL}
AppUpdatesURL={#AppURL}
DefaultDirName={pf}\{#AppName}
DefaultGroupName={#AppName}
OutputDir=../../../target/build
Compression=lzma
SolidCompression=yes
ChangesAssociations=yes
ArchitecturesInstallIn64BitMode=x64

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\deploy\windows\document.ico"; DestDir: {app}/icons;
Source: "..\..\..\target\build\GesticulateFX\*"; DestDir: {app}; Flags: recursesubdirs

[Icons]
Name: "{group}\{#AppName}"; Filename: "{app}\{#AppExeName}"
Name: "{group}\{cm:UninstallProgram,{#AppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#AppName}"; Filename: "{app}\{#AppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#AppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(AppName, "&", "&&")}}"; Flags: nowait postinstall skipifsilent 64bit; Check: IsWin64

[Registry]
Root: HKLM; Subkey: "Software\Classes\.gst"; ValueType: string; ValueData: "GesticulateFile"; Flags: UninsDeleteKeyIfEmpty
Root: HKLM; Subkey: "Software\Classes\.gst\OpenWithProgids"; ValueType: string; ValueName: "GesticulateFXFile"; ValueData: ""; Flags: UninsDeleteValue
Root: HKLM; Subkey: "Software\Classes\GesticulateFXFile"; ValueType: string; ValueData: "Gesticulate Canvas"; Flags: UninsDeleteKey
Root: HKLM; Subkey: "Software\Classes\GesticulateFXFile\DefaultIcon"; ValueType: string; ValueData: "{app}\icons\document.ico";
Root: HKLM; Subkey: "Software\Classes\GesticulateFXFile\shell\open\command"; ValueType: string; ValueData: """{app}\GESTICULATEFX.EXE"" ""%1""";
Root: HKLM; Subkey: "Software\Classes\Applications\{#AppExeName}"; ValueType: string; ValueData: ""; Flags: UninsDeleteKey
Root: HKLM; Subkey: "Software\Classes\Applications\{#AppExeName}\DefaultIcon"; ValueType: string; ValueData: "{app}\icons\document.ico";
Root: HKLM; Subkey: "Software\Classes\Applications\{#AppExeName}\shell\open\command"; ValueType: string; ValueData: """{app}\GESTICULATEFX.EXE"" ""%1""";
Root: HKLM; Subkey: "Software\Classes\Applications\{#AppExeName}\FriendlyAppName"; ValueType: string; ValueData: "{#AppName}";
Root: HKLM; Subkey: "Software\Classes\Applications\{#AppExeName}\SupportedTypes"; ValueType: string; ValueData: ".gst";

Root: HKCR; Subkey: "{#AppName}"; ValueData: "Program {#AppName}"; ValueType: string;  ValueName: ""; Flags: UninsDeleteKey
Root: HKCR; Subkey: "{#AppName}\DefaultIcon"; ValueData: "{app}\{#AppExeName},0"; ValueType: string; ValueName: ""
Root: HKCR; Subkey: "{#AppName}\shell\open\command";  ValueData: """{app}\{#AppExeName}"" ""%1""";  ValueType: string;  ValueName: ""
Root: HKCR; Subkey: ".jpg"; ValueData: "{#AppName}"; ValueType: string; ValueName: ""; Flags: UninsDeleteValue
Root: HKCR; Subkey: ".jpeg"; ValueData: "{#AppName}"; ValueType: string; ValueName: ""; Flags: UninsDeleteValue
Root: HKCR; Subkey: ".gif"; ValueData: "{#AppName}"; ValueType: string; ValueName: ""; Flags: UninsDeleteValue
Root: HKCR; Subkey: ".png"; ValueData: "{#AppName}"; ValueType: string; ValueName: ""; Flags: UninsDeleteValue
Root: HKCR; Subkey: ".svg"; ValueData: "{#AppName}"; ValueType: string; ValueName: ""; Flags: UninsDeleteValue

[Code]

{ ///////////////////////////////////////////////////////////////////// }
function GetUninstallString(): String;
var
  sUnInstPath: String;
  sUnInstallString: String;
begin
  sUnInstPath := ExpandConstant('Software\Microsoft\Windows\CurrentVersion\Uninstall\{#emit SetupSetting("AppId")}_is1');
  sUnInstallString := '';
  if not RegQueryStringValue(HKLM, sUnInstPath, 'UninstallString', sUnInstallString) then
    RegQueryStringValue(HKCU, sUnInstPath, 'UninstallString', sUnInstallString);
  Result := sUnInstallString;
end;


{ ///////////////////////////////////////////////////////////////////// }
function IsUpgrade(): Boolean;
begin
  Result := (GetUninstallString() <> '');
end;


{ ///////////////////////////////////////////////////////////////////// }
function UnInstallOldVersion(): Integer;
var
  sUnInstallString: String;
  iResultCode: Integer;
begin
{ Return Values: }
{ 1 - uninstall string is empty }
{ 2 - error executing the UnInstallString }
{ 3 - successfully executed the UnInstallString }

  { default return value }
  Result := 0;

  { get the uninstall string of the old app }
  sUnInstallString := GetUninstallString();
  if sUnInstallString <> '' then begin
    sUnInstallString := RemoveQuotes(sUnInstallString);
    if Exec(sUnInstallString, '/SILENT /NORESTART /SUPPRESSMSGBOXES','', SW_HIDE, ewWaitUntilTerminated, iResultCode) then
      Result := 3
    else
      Result := 2;
  end else
    Result := 1;
end;

{ ///////////////////////////////////////////////////////////////////// }
procedure CurStepChanged(CurStep: TSetupStep);
begin
  if (CurStep=ssInstall) then
  begin
    if (IsUpgrade()) then
    begin
      UnInstallOldVersion();
    end;
  end;
end;
